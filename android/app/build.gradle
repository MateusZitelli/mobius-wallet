apply plugin: 'com.android.application'

project.ext.envConfigFiles = [
    debug: '.env.development',
    staging: '.env.staging',
    release: '.env.production'
]
apply from: './dotenv.gradle'

apply plugin: 'io.fabric'

/**
 * For configuration options see
 * https://github.com/facebook/react-native/blob/master/local-cli/templates/HelloWorld/android/app/build.gradle#L14
 */
project.ext.react = [
    entryFile: 'index.js',
    inputExcludes: ['android/**', 'ios/**', '.buckd/**'],
    bundleInDebug: false,
    bundleInStaging: true,
    bundleInRelease: true,
    devDisabledInDebug: false,
    devDisabledInStaging: true,
    devDisabledInRelease: true,
    jsBundleDirDebug: "$buildDir/intermediates/assets/debug",
    resourcesDirDebug: "$buildDir/intermediates/res/merged/debug",
    jsBundleDirStaging: "$buildDir/intermediates/assets/staging",
    resourcesDirStaging: "$buildDir/intermediates/res/merged/staging",
    jsBundleDirRelease: "$buildDir/intermediates/assets/release",
    resourcesDirRelease: "$buildDir/intermediates/res/merged/release"

]
apply from: '../../node_modules/react-native/react.gradle'
apply from: '../../node_modules/react-native-code-push/android/codepush.gradle'

/**
 * Add only Material icons
 * https://github.com/oblador/react-native-vector-icons#option-with-gradle-recommended
 */
project.ext.vectoricons = [
    iconFontNames: [ 'MaterialIcons.ttf', 'MaterialCommunityIcons.ttf', 'FontAwesome.ttf' ]
]
apply from: '../../node_modules/react-native-vector-icons/fonts.gradle'

def enableProguardInReleaseBuilds = false

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        applicationId 'network.mobius.wallet'
        applicationIdSuffix ''

        versionCode 26
        versionName '1.1.0'
        versionNameSuffix ''
        resValue 'string', 'app_name', project.env.get('APP_NAME', '@string/app_name_playstore')
        resValue "string", "build_config_package", "network.mobius.wallet"

        manifestPlaceholders = [
            fabricApiKey:project.env.get('CRASHLYTICS_API_TOKEN', '')
        ]

        ndk {
            abiFilters 'armeabi-v7a', 'x86'
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEV'
        }
        staging {
            initWith release
            matchingFallbacks = ['release']
            applicationIdSuffix '.staging'
        }
        release {
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
      transitive = true;
    }
    // From node_modules
    implementation project(':react-native-camera')
    implementation project(':react-native-code-push')
    implementation project(':react-native-device-info')
    implementation project(':react-native-fabric')
    implementation project(':react-native-keychain')
    implementation project(':react-native-languages')
    implementation project(':react-native-linear-gradient')
    implementation project(':react-native-randombytes')
    implementation project(':react-native-splash-screen')
    implementation project(':react-native-svg')
    implementation project(':react-native-vector-icons')
    implementation project(':react-native-version-number')
    implementation 'com.facebook.react:react-native:+'
}

// Run this once to be able to run the application with BUCK
// puts all compile dependencies into folder libs for BUCK to use
task copyDownloadableDepsToLibs(type: Copy) {
    from configurations.compile
    into 'libs'
}
